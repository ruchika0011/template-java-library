plugins {
    id 'java'
    id 'maven-publish'
    id 'pmd'
    id 'io.freefair.lombok' version '6.5.1'
    id 'com.diffplug.spotless' version '6.12.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'org.triplea'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def revisionCount() {
    ['sh', '-c', 'git rev-list --count HEAD'].execute().text.trim()
}
version = "1.0." + revisionCount()

jar {
    archivesBaseName = 'org.triplea.[UPDATE-ME-repo-name]'
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/triplea-game/maps-client")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}


test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        endWithNewline()
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

pmd {
    consoleOutput = true
    ruleSetFiles = files(rootProject.file('.build/pmd.xml'))
    ruleSets = []
    incrementalAnalysis = true
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.1'
}
